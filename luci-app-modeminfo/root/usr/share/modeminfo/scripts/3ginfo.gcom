opengt
 set com 115200n81
 set comecho off
 set senddelay 0.02
 waitquiet 0.2 0.2

 send "ATI5^m"
 get 1 "" $k

 send "AT\^CURC=0^m"
 waitfor 5 "OK","ERROR"

 let $c="AT+CSQ^m"
 let $r="+CSQ"
 gosub readatcmd

 send "AT+CGMR^m"
 get 1 "" $s
 print "\nFW: "$s

 let $c="ATE1; +CGMI^m"
 gosub readatcmdnr
 let $v=$s

 let $c="AT+CGMI^m"
 let $r="+CGMI"
 gosub readatcmd
 let $t=$s

 if $toupper($mid($v,0,3)) = "HUA" goto huawei
 if $toupper($mid($v,0,3)) = "NOK" goto vodafone
 if $toupper($mid($v,0,3)) = "NOV" goto novatel
 if $toupper($mid($v,0,3)) = "OPT" goto option
 if $toupper($mid($v,0,3)) = "QUA" goto qualcomm
 if $toupper($mid($v,0,3)) = "SIM" goto simcom
 if $toupper($mid($v,0,3)) = "SIE" goto sierra
 if $toupper($mid($v,0,3)) = "VOD" goto vodafone
 if $toupper($mid($v,0,3)) = "ZTE" goto zte
 if $toupper($mid($v,0,3)) = "QUE" goto quectel
 if $toupper($mid($v,0,3)) = "DEL" goto dell
 if $toupper($mid($k,8,3)) = "XMM" goto intel
 if $toupper($mid($t,9,3)) = "MIK" goto mikrotik
 goto generic

:simcom
 let $c="AT+CNSMOD?^m"
 let $r="+CNSMOD"
 gosub readatcmd

 let $c="AT+CPSI?^m"
 let $r="+CPSI"
 gosub readatcmd

 let $c="AT+CPMUTEMP^m"
 let $r="+CPMUTEMP"
 gosub readatcmd
 goto next

:vodafone
 let $c="AT%NWSTATE=1^m"
 let $r="%NWSTATE"
 gosub readatcmd
 goto next

:novatel
 let $c="AT$CNTI=0^m"
 let $r="$CNTI"
 gosub readatcmd
 goto next

:sierra
 let $c="AT*CNTI=0^m"
 let $r="*CNTI"
 gosub readatcmd

 send "AT!GSTATUS?^m"
 get 1 "" $s
 print $s
 goto next

:mikrotik
 let $c="AT+COPS?^m"
 let $r="+COPS"
 gosub readatcmd
# Returns received signal quality parameters
 let $c="AT+CESQ^m"
 let $r="+CESQ:"
 gosub readatcmd

 let $c="AT+CEREG?^m"
 let $r="+CEREG"
 gosub readatcmd
# Return information of model
 let $c="AT+CGMM^m"
 let $r="+CGMM"
 gosub readatcmd
# Return information of serial number
 send "AT+CGSN^m"
 get 1 "" $s
 print '\n',"+CGSN: ",$mid($s,25,18)
# Activate query mode
 send "AT+EEMOPT=1^m"
# Query LTE information in Engineer
 let $c="AT+EEMGINFO?^m"
 let $r="+EEMLTESVC:"
 gosub readatcmd
# Query UMTS information in Engineer
 let $c="AT+EEMGINFO?^m"
 let $r="+EEMUMTSSV:"
 gosub readatcmd
# Query GSM information in Engineer
 let $c="AT+EEMGINFO?^m"
 let $r="+EEMGINFOSVC:"
 gosub readatcmd
 goto next

:option
 let $c="AT_OCTI?^m"
 let $r="_OCTI"
 gosub readatcmd

 let $c="AT_OWCTI?^m"
 let $r="_OWCTI"
 gosub readatcmd
 goto next
 
:quectel
 let $c="AT+COPS=3,2;+COPS?^m"
 let $r="+COPS"
 gosub readatcmd

 let $c="AT+QENG=\"servingcell\"^m"
 let $r="+QENG"
 gosub readatcmd

 let $c="AT+QTEMP^m"
 let $r="+QTEMP"
 gosub readatcmd

 send "AT+QCAINFO^m"
 get 1 "" $s
 print $s
 goto next1
 
:qualcomm
 let $c="AT\^RFSTS?^m"
 let $r="\^RFSTS"
 gosub readatcmd

 send "AT+TEMP^m"
 get 1 "" $s
 print $s

 send "AT\^CA_INFO?^m"
 get 1 "" $s
 print $s
 goto next

:dell
 send "AT\^DEBUG?^m"
 get 1 "" $s
 print $s

 send "AT+TEMP^m"
 get 1 "" $s
 print $s

 send "AT\^TEMP?^m"                             
 get 1 "" $s                                    
 print $s

 send "AT\^CA_INFO?^m"
 get 1 "" $s
 print $s
 goto next

:intel
 print "\n",$toupper($mid($k,8,3)),"\n"

 let $c="AT+COPS=3,2;+COPS?^m"
 let $r="+COPS"
 gosub readatcmd

 let $c="AT+XCESQ?^m"
 let $r="+XCESQ"
 gosub readatcmd

 let $c="AT+RSRP?^m"
 let $r="+RSRP"
 gosub readatcmd

 let $c="AT+RSRQ?^m"
 let $r="+RSRQ"
 gosub readatcmd

 let $c="AT+RSCP?^m"
 let $r="+RSCP"
 gosub readatcmd

 let $c="AT+XLEC?^m"
 let $r="+XLEC"
 gosub readatcmd

 let $c="AT+MTSM=1^m"
 let $r="+MTSM"
 gosub readatcmd

 let $c="AT+XMCI=1^m"
 let $r="+XMCI"
 gosub readatcmd
 goto next1

:zte
 let $c="AT+COPS?^m"
 let $r="+COPS"
 gosub readatcmd

 let $c="AT+ZRSSI^m"
 let $r="+ZRSSI"
 gosub readatcmd

 let $c="AT+ZPAS?^m"
 let $r="+ZPAS"
 gosub readatcmd
 goto next1

:huawei
# uppercase huawei to Huawei
 let $v=$toupper($left($v,1))+$mid($v,1,len($v)-1)

 let $c="AT\^FHVER^m"
 let $r="\^FHVER"
 gosub readatcmd

 if $s="\nERROR" goto jumprm
# remove ^FHVER:" from start of string & " from end of string
 let $s=$mid($s,9,len($s)-10)
 gosub strfindfspace
 let $m=$mid($s,0,i)

:jumprm
 let $c="AT\^SYSINFOEX^m"
 let $r="\^SYSINFOEX"
 gosub readatcmd

 let $c="AT\^CSNR?^m"
 let $r="\^CSNR"
 gosub readatcmd

 let $c="AT\^LTERSRP?^m"
 let $r="\^LTERSRP"
 gosub readatcmd

 let $c="AT\^HCSQ?^m"
 let $r="\^HCSQ"
 gosub readatcmd

 let $c="AT\^HFREQINFO?^m"
 let $r="\^HFREQINFO"
 gosub readatcmd

 let $c="AT\^CHIPTEMP?^m"
 let $r="\^CHIPTEMP"
 gosub readatcmd

 let $c="AT+CSCA?^m"
 let $r="+CSCA"
 gosub readatcmd

 let $c="AT\^SYSCFG?^m"
 let $r="\^SYSCFG"
 gosub readatcmd

 let $c="AT\^SYSCFGEX?^m"
 let $r="\^SYSCFG"
 gosub readatcmd

 let $c="AT+CGPADDR=1^m"
 let $r="+CGPADDR"
 gosub readatcmd

:generic
 let $c="AT\^SYSINFO^m"
 let $r="\^SYSINFO"
 gosub readatcmd

:next
 let $c="AT+COPS=3,2;+COPS?^m"
 let $r="+COPS"
 gosub readatcmd

:next1
 let $c="AT+CREG=2;+CREG?^m"
 let $r="+CREG"
 gosub readatcmd

 let $c="AT+CGREG=2;+CGREG?^m"
 let $r="+CGREG"
 gosub readatcmd

 let $c="AT+CEREG=2;+CEREG?^m"
 let $r="+CEREG"
 gosub readatcmd

 let $c="AT+CGEQNEG=1^m"
 let $r="+CGEQNEG"
 gosub readatcmd

 let $c="AT+CGMM^m"
 gosub readatcmdnr
 if $m = "" print "\nDEVICE:",$v," ",$s
 else print "\nDEVICE:",$v," ",$m

 let $c="AT+CGSN^m"
# let $r="+CGSN"
 gosub readatcmdnr
 print "\nIMEI:",$s,"\n"

 exit 0

:readatcmd
 let i=15
 send $c
:loop
 get 0.1 "^m" $s
 let l=len($r)
 if len($s) < l goto loop1
 if $mid($s,1,l) <> $r goto loop1
 print $s
 return

:loop1
 if len($s) < 2 goto loop2
 if $mid($s,1,2) = "ER" return
 if $mid($s,1,2) = "CO" return
:loop2
 if i = 0 return
 let i=i-1
 goto loop

:readatcmdnr
 let i=15
 send $c
:loop3
 get 0.1 "^m" $s
 if len($s) < 2 goto loop5
 if $mid($s,1,2) = "ER" goto loop4
 if $mid($s,1,2) = "OK" goto loop5
 if $mid($s,1,2) = "AT" goto loop5
 if $mid($s,1,2) = "TE" goto loop5
 if $mid($s,1,1) = "+"  goto loop5
 if $mid($s,1,1) = "\^"  goto loop5
 let l=len($s)
 let $s=$mid($s,1,l)
 return

:loop4
 let $s=""
 return

:loop5
 if i = 0 return
 let i=i-1
 goto loop3

:strfindfspace
 let i=0
 let l=len($s)
 if l = 0 return
:loop6
 if i = l return
 if $mid($s,i,1) = " " return
 let i=i+1
 goto loop6
